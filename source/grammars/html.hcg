# 
# Wick HTML Parser file
# 

@NAME CandleFW Hyper Text Markup Langauge Parser

@EXT html

@SYMBOL </ <! -- 

@IGNORE θws θnl 

@IMPORT ./string.hcg as string

##############################################################################################
##############################################################################################
#### HTML / ML
##############################################################################################
##############################################################################################
    
<> HTML → RESULT

<> RESULT → DTD(*) BODY                                                                     ↦r { $sym2.DTD = $sym1, $sym2 }

<> BODY → GOAL_TAG

<> GOAL_TAG → TAG

<> TAG → < TAGNAME ATTRIBUTES? > TAG_BODY? </ TAGNAME >                                     ↦r { { type:"HTML", tag:$sym2.topUpperCase(), attributes:$$sym3 || [], children:$sym5 || [], VOID:false, lex } }
    │ < TAGNAME ATTRIBUTES? / >                                                             ↦r { { type:"HTML", tag:$sym2.topUpperCase(), attributes:$$sym3 || [], children:[], VOID:false, lex } }
    │ < VOID_NAME ATTRIBUTES? /? >                                                          ↦r { { type:"HTML", tag:$sym2.topUpperCase(), attributes:$$sym3 || [], children:[], VOID:true, lex } }
    
<> DTD → <! θid? --?  (BODY_SYMBOLS)(*")  --? >                                             ↦r { { type:"DTD", tag:$sym1,  }  }                


###########################################################################################
# https://www.w3.org/TR/2011/WD-html-markup-20110113/syntax.html#syntax-elements
###########################################################################################

<> VOID_NAME → τinput
    │ τarea
    │ τbase
    │ τbr
    │ τcol
    │ τcommand
    │ τembed
    │ τhr
    │ τimg
    │ τkeygen
    │ τlink
    │ τmeta
    │ τparam
    │ τsource
    │ τtrack
    │ τwbr

<> TAG_BODY → TAG_BODY_ITEM(+)

<> TAG_BODY_ITEM → TEXT_NODE                        
    │ TAG   
    │ DTD                                                                                                                            
    
<> ATTRIBUTES → ATTRIBUTE(+)                                                                    ↦r { $$sym1 }

<> ATTRIBUTE → ATTRIBUTE_HEAD = ATTRIBUTE_BODY                                                  ↦r { {type:"ATTRIBUTE", name:$sym1, value:$sym3, IS_BINDING:false, lex } }
    │ ATTRIBUTE_HEAD                                                                            ↦r { {type:"ATTRIBUTE", name:$sym1, value:"", IS_BINDING:false, lex } }

<> ATTRIBUTE_HEAD → ' identifier? '                                                             ↦r { $$sym2 }
    │ " identifier? "                                                                           ↦r { $$sym2 }
    │ identifier


<> ATTRIBUTE_BODY → string::normal_string                       
    │ identifier
    │ θnum                                          

<> TAGNAME → (RED θnl) identifier


<> identifier → (θid) ( θid │ \- │ \_ │ \: )(*")                                  				↦r { $sym1 + $sym2 } 


<> TEXT_NODE → BODY_SYMBOLS(+")                                                                 ↦r { {type:"TEXT", data:$sym1, IS_BINDING:false, lex } }

<> BODY_SYMBOLS → θid │ θws │ θsym │ θop │ θob │ θcb │ θnum │ θkeyword │ θany │ \! │ \(





